plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.gina'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation("org.jsoup:jsoup:1.20.1")
	implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.12.5'
	implementation 'org.postgresql:postgresql:42.7.5'
	implementation 'org.liquibase:liquibase-core:4.31.1'
	implementation "io.netty:netty-resolver-dns"
	implementation "io.netty:netty-resolver-dns-native-macos:4.1.109.Final:osx-aarch_64"



	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.12.5'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.12.5'

	compileOnly 'org.projectlombok:lombok'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('npmInstall', Exec) {
	workingDir 'src/main/client'
	commandLine 'npm', 'install'
}

tasks.register('npmBuild', Exec) {
	dependsOn tasks.named('npmInstall')
	workingDir 'src/main/client'
	commandLine 'npm', 'run', 'build'
}

tasks.register('copyFrontend', Copy) {
	dependsOn tasks.named('npmBuild')
	from 'src/main/client/dist'
	into 'src/main/resources/static'
}

bootJar {
	dependsOn tasks.named('copyFrontend')
}

tasks.named('test') {
	useJUnitPlatform()
}
